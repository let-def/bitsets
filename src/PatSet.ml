type t = {
  k: int;
  v: int;
  l: t;
  r: t;
}

type elt = int

let rec empty = {k = 0; v = 0; l = empty; r = empty}

(* Extract the most significant bit of integer `x` *)
let extract_bit = Bit_lib.extract_msb

let rec equal t1 t2 =
  t1.k = t2.k && t1.v = t2.v &&
  (t1.l == t2.l || equal t1.l t2.l) &&
  (t1.r == t2.r || equal t1.r t2.r)

let equal t1 t2 = t1 == t2 || equal t1 t2

let rec compare t1 t2 =
  if t1 == t2 then 0 else
  let c = Int.compare t1.k t2.k in
  if c <> 0 then c else
    let c = Int.compare t1.v t2.v in
    if c <> 0 then c else
      let c = compare t1.l t2.l in
      if c <> 0 then c else (
        let c = compare t1.r t2.r in
        if c <> 0 then c else
          0
      )

let rec lookup k t =
  if k >= t.k then
    if k = t.k
    then t.v
    else 0
  else
    let k' = k land lnot (extract_bit t.k) in
    lookup k' (if k = k' then t.r else t.l)

let rec insert k v t =
  if k >= t.k then
    if k = t.k then
      match v lor t.v with
      | v when v = t.v -> t
      | v -> {t with v}
    else
      let m = extract_bit k in
      if m land t.k = m then
        {k; v; l = insert (t.k land lnot m) t.v t.l; r = t.r}
      else
        {k; v; l = empty; r = t}
  else
    let m = extract_bit k in
    if m > t.k lsr 1 then
      match insert (k land lnot m) v t.l with
      | l when t.l == l -> t
      | l -> {t with l}
    else
      match insert k v t.r with
      | r when t.r == r -> t
      | r -> {t with r}

let _low_level_insert_mask = insert

let rec join k l r =
  match l.v with
  | 0 -> r
  | v ->
    let l' = join l.k l.l l.r in
    {k = (extract_bit k) lor l.k; v; l = l'; r}

let rec remove k v t =
  if k >= t.k then
    if k = t.k then
      match t.v land lnot v with
      | 0 -> join t.k t.l t.r
      | v -> if v = t.v then t else {t with v}
    else
      t
  else
    let m = extract_bit k in
    if m > t.k lsr 1 then
      match remove (k land lnot m) v t.l with
      | l when t.l == l -> t
      | l -> {t with l}
    else
      match remove k v t.r with
      | r when t.r == r -> t
      | r -> {t with r}

let rec union a b =
  let a, b = if a.k > b.k then b, a else a, b in
  (* b.k >= a.k *)
  if a.v = 0 then b else
  if a.k = b.k then
    let l = union a.l b.l in
    let r = union a.r b.r in
    match a.v lor b.v with
    | v when v = b.v && l == b.l && r == b.r -> b
    | v -> {b with v; l; r}
  else
    let m = extract_bit a.k in
    if m > b.k lsr 1 then
      match
        (* TODO: insert_and_union? *)
        union_fringe (lnot m) a a.l b.l,
        union a.r b.r
      with
      | l, r when l == b.l && r == b.r -> b
      | l, r -> {b with l; r}
    else
      match union a b.r with
      | r when r == b.r -> b
      | r -> {b with r}

and union_fringe mask a0 a b =
  if a0 == a then union a b
  else
    let k = a0.k land mask in
    if k = b.k then
      (* b.k > a.k *)
      let m = extract_bit a.k in
      let v = a0.v lor b.v in
      let l, r =
        let mask = mask land lnot m in
        if m > b.k lsr 1 then
          union_fringe mask a0.l a.l b.l,
          union a.r b.r
        else
          insert_fringe mask a0.l a b.l,
          union a b.r
      in
      if v = b.v && l == b.l && r == b.r then
        b
      else
        {k; v; l; r}
    else if k > b.k then
      union_fringe_safe mask a0 a b
      (*let m = extract_bit k in
        let v = a0.v in
        if b.k land m = m (* same bit *) then
        {k; v; l = union_fringe_safe (mask land lnot m) a0.l a b.l; r = b.r}
        else
        {k; v; l = empty; r = union_fringe mask a0.l a b}*)
    else
      let m = extract_bit b.k in
      if a.k land m = m then
        (*same bit*)
        match
          union_fringe_safe (mask land lnot m) a0 a.l b.l,
          union a.r b.r
        with
        | l, r when l == b.l && r == b.r -> b
        | l, r -> {b with l; r}
      else
        let mask = mask land lnot m in
        let rec extract_fringe a0 a l =
          if a0.k land m = m then
            extract_fringe a0.l a (insert (a0.k land mask) a0.v l)
          else
            let r = union_fringe mask a0 a b.r in
            if l == b.l && r == b.r
            then b
            else {b with l; r}
        in
        extract_fringe a0 a b.l

and union_fringe_safe mask a0 a b =
  union a (insert_fringe mask a0 a b)

and insert_fringe mask a0 a b =
  if a0 == a then
    b
  else
    insert_fringe mask a0.l a (insert (a0.k land mask) a0.v b)

let diff_update a v l r =
  if v = 0 then
    join a.k l r
  else if v = a.v && a.l == l && a.r == r then
    a
  else
    {a with v; l; r}

let rec diff a b =
  if b.v = 0 then a else
  if a.k >= b.k then
    if a.k = b.k then
      diff_update a
        (a.v land lnot b.v)
        (diff a.l b.l)
        (diff a.r b.r)
    else
      let m = extract_bit a.k in
      if b.k land m = m then
        diff_update a a.v
          (* TODO: remove_and_diff?
             skip to b.k land m -1, remove b.l *)
          (diff (remove (b.k land lnot m) b.v a.l) b.l)
          (diff a.r b.r)
      else
        diff_update a a.v a.l (diff a.r b)
  else
    let m = extract_bit a.k in
    if m > b.k lsr 1 then
      diff_update a
        (a.v land lnot (lookup (a.k land lnot m) b.l))
        (diff a.l b.l)
        (diff a.r b.r)
    else
      diff a b.r

let rec inter a b =
  if a == b then
    a
  else if a.k > b.k then
    inter_right b a
  else
    inter_right a b

and inter_right a b =
  (* a.k <= b.k *)
  if b.v = 0 then empty else
  if a.k = b.k then
    let l = inter a.l b.l in
    let r = inter a.r b.r in
    match a.v land b.v with
    | 0 -> join a.k l r
    | v -> {k = a.k; v; l; r}
  else
    (* a.k < b.k *)
    let m = extract_bit a.k in
    if m = extract_bit b.k then
      (* same msb *)
      let l = inter a.l b.l in
      let r = inter a.r b.r in
      match lookup (a.k lxor m) b.l with
      | 0 -> join a.k l r
      | v -> {k = a.k; v; l; r}
    else
      (* a.k << b.k *)
      inter a b.r

let mask_size = Sys.word_size - 1

let encode x = (x / mask_size, 1 lsl (x mod mask_size))

let decode_base x = x * mask_size

let add x t =
  let k, v = encode x in
  insert k v t

let mem x t =
  let k, v = encode x in
  lookup k t land v <> 0

let remove x t =
  let k, v = encode x in
  remove k v t

let singleton x = add x empty

let rec iter f mask t =
  if t.v <> 0 then (
    iter f mask t.r;
    iter f (mask lor extract_bit t.k) t.l;
    let base = decode_base (mask lor t.k) in
    let v = ref t.v in
    for _ = 0 to Bit_lib.pop_count t.v - 1 do
      let index = Bit_lib.lsb_index !v in
      v := !v lxor (1 lsl index);
      f (base + index);
    done
  )

let iter f t = iter f 0 t

let rec fold f mask t acc =
  if t.v <> 0 then (
    let acc = fold f mask t.r acc in
    let acc = fold f (mask lor extract_bit t.k) t.l acc in
    let base = decode_base (mask lor t.k) in
    let v = ref t.v in
    let acc = ref acc in
    for _ = 0 to Bit_lib.pop_count t.v - 1 do
      let index = Bit_lib.lsb_index !v in
      v := !v lxor (1 lsl index);
      acc := f (base + index) !acc;
    done;
    !acc
  ) else
    acc

let fold f t acc = fold f 0 t acc

let rec rev_iter f mask t =
  if t.v <> 0 then (
    let base = decode_base (mask lor t.k) in
    let v = ref t.v in
    for _ = 0 to Bit_lib.pop_count t.v - 1 do
      let index = Bit_lib.msb_index !v in
      v := !v lxor (1 lsl index);
      f (base + index);
    done;
    rev_iter f (mask lor extract_bit t.k) t.l;
    rev_iter f mask t.r;
  )

let _rev_iter f t = rev_iter f 0 t

let is_empty t = t.v = 0

let is_singleton t =
  t.v <> 0 &&
  (t.v land (t.v - 1)) lor t.l.v lor t.r.v = 0

let check _ = ()

let elements s = fold (fun x xs -> x :: xs) s []

let sorted_union xs = List.fold_left union empty xs

let rec disjoint t1 t2 =
  t1.v = 0 || t2.v = 0 || (
    t1 != t2 &&
    if t1.k = t2.k then (
      t1.v land t2.v = 0 &&
      disjoint t1.l t2.l &&
      disjoint t1.r t2.r
    ) else
      let msb1 = extract_bit t1.k in
      let msb2 = extract_bit t2.k in
      if msb1 = msb2 then (
        if t1.k > t2.k then
          lookup t2.k t1 land t2.v = 0 &&
          disjoint t1.l t2.l &&
          disjoint t1.r t2.r
        else
          lookup t1.k t2 land t1.v = 0 &&
          disjoint t1.l t2.l &&
          disjoint t1.r t2.r
      ) else if t1.k > t2.k then
        disjoint t1.r t2
      else
        disjoint t1 t2.r
  )

let cardinal t =
  let rec loop t acc =
    if t.v = 0 then
      acc
    else
      let acc = acc + Bit_lib.pop_count t.v in
      let acc = loop t.l acc in
      let acc = loop t.r acc in
      acc
  in
  loop t 0

let rec minimum mask t =
  if t.r.v <> 0 then
    minimum mask t.r
  else if t.l.v <> 0 then
    minimum (mask lor extract_bit t.k) t.l
  else
    decode_base (mask lor t.k) + Bit_lib.lsb_index t.v

let minimum t =
  if t.v = 0 then
    raise Not_found;
  minimum 0 t

let maximum t =
  if t.v = 0 then
    raise Not_found;
  decode_base t.k + Bit_lib.msb_index t.v

let choose t = minimum t (*TODO: Later make it maximum, it is faster, but testsuite assumes minimum*)

let quick_subset a b =
  lookup a.k b land a.v <> 0

let compare_minimum a b =
  match a.v, b.v with
  | 0, 0 -> 0
  | 0, _ -> -1
  | _, 0 -> 1
  | _ -> Int.compare (minimum a) (minimum b)

let above x t =
  let k, v = encode x in
  let mask = -v lsl 1 in
  let rec loop k t =
    if t.v = 0 || t.k < k then
      empty
    else if t.k = k then
      match t.v land mask with
      | 0 -> empty
      | v -> {k; v; l = empty; r = empty}
    else
      (* t.k > k *)
      let msb = extract_bit t.k in
      if k land msb = msb then
        (* same msb *)
        let l = loop (k lxor msb) t.l in
        {t with l; r = empty}
      else
        (* lower msb *)
        {t with r = loop k t.r}
  in
  loop k t

let rec subset sub sup =
  sub.v = 0 || (
    sup.v <> 0 &&
    if sub.k = sup.k then
      sub.v land sup.v = sub.v &&
      subset sub.l sup.l &&
      subset sub.r sup.r
    else
      sub.k < sup.k &&
      let msb = extract_bit sup.k in
      if sub.k land msb = msb then
        (* Same msb *)
        lookup (sub.k lxor msb) sup.l land sub.v = sub.v &&
        subset sub.l sup.l &&
        subset sub.r sup.r
      else
        subset sub sup.r
  )

(*let rec find_first f mask t =
  match t.v with
  | 0 -> -1
  | v ->
    let base = decode_base (mask lor t.k) in
    let i = Bit_lib.lsb_index v in
    let x = base + i in
    if f x then
      match find_first f mask t.r with
      | -1 ->
        (* Try in the left *)
        begin match find_first f (mask lor (extract_bit t.k)) t.l with
          | -1 -> x
          | x' -> x'
        end
      | x' -> x'
    else
      (* Try in other elements of this node *)
      let j = Bit_lib.msb_index v in
      if f (base + j) then
        let v = ref (v lxor ((1 lsl i) lor (1 lsl j))) in
        let j = ref j in
        while !v <> 0 do
          let j' = Bit_lib.extract_msb !v in
          if f (base + j') then
            (j := j'; v := !v lxor (1 lsl j'))
          else
            v := 0
        done;
        base + !j
      else
        -1

let find_first_opt f t =
  match find_first f 0 t with
  | -1 -> None
  | x -> Some x*)

exception Found of int

let find_first_opt f t =
  try
    iter (fun x -> if f x then raise (Found x)) t;
    None
  with Found x -> Some x

(* TODO *)

let rec minimum_leaf mask t =
  if t.r.v <> 0 then
    minimum_leaf mask t.r
  else if t.l.v <> 0 then
    minimum_leaf (mask lor extract_bit t.k) t.l
  else
    (mask lor t.k, t.v)


let extract_unique_prefix s1 mink minv =
  let rec aux mask s1 =
    let k = s1.k lor mask in
    if k < mink then
      (s1, empty)
    else if k = mink then
      let mask = Bit_lib.extract_lsb minv - 1 in
      match s1.v land mask, s1.v land lnot mask with
      | 0, vl ->
        let lo = s1.r in
        let hi = {s1 with v = vl; r = empty} in
        (lo, hi)
      | vr, 0 ->
        let lo = {s1 with v = vr; l = empty} in
        let hi = join s1.k s1.l empty in
        (lo, hi)
      | vr, vl ->
        let lo = {s1 with v = vr; l = empty} in
        let hi = {s1 with v = vl; r = empty} in
        (lo, hi)
    else
      (* k > mink *)
      let msb = extract_bit k in
      if mink land msb = k (* same msb *) then
        let lo, hi = aux (mask lor msb) s1.l in
        (join s1.k lo s1.r, {s1 with l = hi; r = empty})
      else
        let lo, hi = aux mask s1.r in
        (lo, {s1 with r = hi})
  in
  aux 0 s1

let extract_unique_prefix s1 s2 =
  let mink, minv = minimum_leaf 0 s2 in
  extract_unique_prefix s1 mink minv

let extract_shared_prefix _ _ = (empty, (empty, empty))
